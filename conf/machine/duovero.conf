#@TYPE: Machine
#@NAME: DuoVero
#@DESCRIPTION: Machine configuration for the Gumstix DuoVero

SOC_FAMILY = "omap4"
TARGET_ARCH = "arm"
# We target cortexa8hf-neon rather than cortexa9hf-neon as the only difference
# is -mtune=cortexa9 and this allows us to be consistent with our Cortex-A8
# builds for Pepper and Overo.
DEFAULTTUNE ?= "cortexa8hf-neon"

require conf/machine/include/soc-family.inc
require conf/machine/include/tune-cortexa8.inc

# Specify kernel recipe
PREFERRED_PROVIDER_virtual/kernel ?= "linux-gumstix"

# Specify u-boot parameters
PREFERRED_PROVIDER_virtual/bootloader = "u-boot"
PREFERRED_PROVIDER_u-boot = "u-boot"

UBOOT_ENTRYPOINT = "0x80008000"
UBOOT_LOADADDRESS = "0x80008000"
UBOOT_MACHINE = "duovero_config"
SPL_BINARY = "MLO"
UBOOT_SUFFIX = "img"

EXTRA_IMAGEDEPENDS += "u-boot"

# Specify files for the boot partition for wic
# Note: if UBOOT_ENV is set, it should also be included here
IMAGE_BOOT_FILES ?= "u-boot.${UBOOT_SUFFIX} ${SPL_BINARY}"

# Specify xserver components for builds including X11
PREFERRED_PROVIDER_virtual/xserver = "xserver-xorg"
XSERVER = "xserver-xorg \
           xserver-xorg-module-exa \
           xf86-input-evdev \
           xf86-input-keyboard \
           xf86-input-mouse \
           xf86-video-fbdev \
           xf86-video-omapfb \
           xf86-video-omap"

SERIAL_CONSOLES = "115200;ttyO2"

# tar.b2 for SD card
IMAGE_FSTYPES ?= "tar.bz2"

# Include wifi firmware
MACHINE_EXTRA_RDEPENDS += "linux-firmware-sd8787 linux-firmware-wl18xx"

# Ship all kernel modules by default
MACHINE_EXTRA_RRECOMMENDS = " kernel-modules"
MACHINE_FEATURES = "alsa bluetooth ext2 keyboard serial touchscreen usbgadget usbhost vfat wifi"
